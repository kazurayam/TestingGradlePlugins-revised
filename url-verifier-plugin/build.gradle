plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group 'org.myorg'
version '1.2'

repositories {
    mavenCentral()
}

def integrationTest = sourceSets.create("integrationTest")
def integrationTestTask = tasks.register("integrationTest", Test) {
    description = "Runs the integration tests."
    testClassesDirs = integrationTest.output.classesDirs
    classpath = integrationTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}

def functionalTest = sourceSets.create("functionalTest")
def functionalTestTask = tasks.register("functionalTest", Test) {
    description = "Runs the functional tests."
    group = "verification"
    testClassesDirs = functionalTest.output.classesDirs
    classpath = functionalTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}

tasks.named('check') {
    dependsOn(integrationTestTask, functionalTestTask)
}

dependencies {
    // we will use Spock frame for testing
    testImplementation platform(libs.spock.bom)
    testImplementation libs.spock.core
    integrationTestImplementation platform(libs.spock.bom)
    integrationTestImplementation libs.spock.core
    functionalTestImplementation platform(libs.spock.bom)
    functionalTestImplementation libs.spock.core

    // What is `platform` ?
    // https://docs.gradle.org/current/userguide/platforms.html#sub:using-platform-to-control-transitive-deps
    // Using a platform to control transitive versions
    // A platform is a special software component which can be used to
    // control transitive dependency versions.

    // let the integrationTest refer to the class files
    // of the `main` sourceSet.
    integrationTestImplementation sourceSets.main.output
}


gradlePlugin {
    // configure the `java-gradle-plugin` so that it looks at the `sourceSets.functionalTest`
    // to find the tests for the custom plugin.
    testSourceSets(sourceSets.functionalTest)
    // This makes `org.gradle.testkit.runner.GradleRunner` class available to the
    // functionalTest classes.
}



tasks.withType(Test).configureEach {
    // Using JUnitPlatform for running tests written using JUnit5 API
    useJUnitPlatform()
}


